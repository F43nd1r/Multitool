plugins {
    id 'com.android.application'
    id 'com.github.triplet.play'
    id 'com.faendir.gradlekeepass' version '0.1'
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.faendir.lightning_launcher.multitool"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode Integer.parseInt(rootProject.versionCode)
        versionName version
    }
    signingConfigs {
        release {
            storeFile file(androidKeyStoreFile)
            def login = keepass.getLogin('intellij.android.key')
            storePassword login.Password
            keyAlias login.Login
            keyPassword login.Password
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file("lint.xml")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    jsonFile = file(playKeyFile)
    track = version.contains('b') ? 'beta' : 'production'
}

dependencies {
    def supportVersion = '1.0.0-rc01'
    def acraVersion = '5.2.0-rc2'
    implementation 'com.faendir.lightninglauncher:scriptlib:3.0'
    implementation "androidx.appcompat:appcompat:$supportVersion"
    implementation "androidx.recyclerview:recyclerview:$supportVersion"
    implementation "androidx.preference:preference:$supportVersion"
    implementation "com.google.android.material:material:$supportVersion"
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-limiter:$acraVersion"
    implementation "ch.acra:acra-advanced-scheduler:$acraVersion"
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    implementation 'com.mikepenz:materialdrawer:6.0.9'
    implementation "com.mikepenz:fastadapter-extensions:3.2.8"
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.6.3'
    implementation 'org.mozilla:rhino:1.7.9'
    implementation 'com.evernote:android-job:1.2.6'
    compileOnly "com.google.auto.service:auto-service:1.0-rc4"
    annotationProcessor "com.google.auto.service:auto-service:1.0-rc4"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
}

task increaseVersionNumber {
    doLast {
        rootProject.versionCode = (Integer.parseInt(rootProject.versionCode) + 1)
        android.defaultConfig.setVersionCode(rootProject.versionCode)
        ant.propertyfile(file: "../gradle.properties") {
            entry(key: "versionCode", value: rootProject.versionCode)
        }
    }
}


afterEvaluate {
    rootProject.confirmReleaseVersion.dependsOn(increaseVersionNumber)
    rootProject.beforeReleaseBuild.dependsOn(clean)
    rootProject.build.dependsOn(build)
    rootProject.afterReleaseBuild.dependsOn(publishApkRelease)
}
